PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ddd: <https://ggen.io/ontology/ddd#>
PREFIX attr: <https://ggen.io/examples/attribution#>

SELECT DISTINCT
  ?aggregate
  ?aggregateName
  ?aggregateComment
  ?fieldName
  ?fieldType
  ?fieldRequired
  ?fieldDefault
  ?fieldComment
WHERE {
  VALUES ?aggregate {
    attr:RevenueAggregate
    attr:PublisherRevenueAggregate
  }

  ?aggregate a ddd:Aggregate ;
    ddd:name ?aggregateName .

  OPTIONAL { ?aggregate rdfs:comment ?aggregateComment }

  ?aggregate ddd:hasField ?field .

  ?field ddd:name ?fieldName ;
    ddd:type ?fieldType .

  OPTIONAL { ?field ddd:required ?fieldRequired }
  OPTIONAL { ?field ddd:default ?fieldDefault }
  OPTIONAL { ?field rdfs:comment ?fieldComment }
}
ORDER BY ?aggregateName ?fieldName
